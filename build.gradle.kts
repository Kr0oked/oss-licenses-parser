/*
 * This file is part of OssLicensesParser.
 * Copyright (C) 2024 Philipp Bobek <philipp.bobek@mailbox.org>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * OssLicensesParser is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    kotlin("jvm") version "1.9.22"
    id("org.jetbrains.dokka") version "1.9.20"
    `java-library`
    `maven-publish`
    signing
}

group = "de.philipp-bobek"
version = "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

kotlin {
    jvmToolchain(17)
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        create<MavenPublication>("OssLicensesParser") {
            from(components["java"])
            pom {
                name = "OssLicensesParser"
                description = "Parser for the text files generated by the OSS Licenses Gradle Plugin"
                url = "https://github.com/Kr0oked/oss-licenses-parser"
                licenses {
                    license {
                        name = "LGPL-3.0-or-later"
                        url = "https://www.gnu.org/licenses/lgpl+gpl-3.0.txt"
                        comments = "GNU Lesser General Public License v3.0 or later"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "philipp.bobek"
                        name = "Philipp Bobek"
                        email = "philipp.bobek@mailbox.org"
                        url = "https://philipp-bobek.de"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/Kr0oked/oss-licenses-parser.git"
                    developerConnection = "scm:git:https://github.com/Kr0oked/oss-licenses-parser.git"
                    url = "https://github.com/Kr0oked/oss-licenses-parser"
                }
            }
        }
    }
    repositories {
        maven {
            name = "build"
            url = uri(layout.buildDirectory.dir("repo"))
        }
    }
}

signing {
    sign(publishing.publications["OssLicensesParser"])
}

tasks.jar {
    manifest {
        attributes(
            mapOf(
                "Implementation-Title" to project.name,
                "Implementation-Version" to project.version
            )
        )
    }
}

tasks.named<Jar>("javadocJar") {
    from(tasks.named("dokkaJavadoc"))
}

tasks.test {
    useJUnitPlatform()
}
